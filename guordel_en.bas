#program guordel_en
#autostart

   1 REM Copyright (c) 2022, kounch. All rights reserved.
   2 REM SPDX-License-Identifier: BSD-2-Clause

  10 LET I=NOT PI:LET P=VAL"7":LET O=VAL"4":LET K=VAL"2":LET Y=VAL"6"
  20 LET A=VAL"48126":LET B=VAL"48127":LET C=VAL"41716":LET D=VAL"48119":LET E=VAL"45566":LET F=VAL"44788"

  99 REM Get lists length
 100 PAPER P:BORDER P:INK I:CLS:PRINT AT VAL"2",VAL"8";"** ZX GUORDEL **"
 110 PRINT AT VAL"8",VAL"7";"COUNTING WORDS...";:POKE A,NOT PI:POKE B,NOT PI:RANDOMIZE USR C:PRINT "OK"
 120 LET M=PEEK A*VAL"256"+PEEK B:PRINT AT VAL"10",VAL"10";"FOUND ";M
 130 LET N=PEEK E*VAL"256"+PEEK (E+VAL"1"):PRINT AT VAL"12",VAL"9";N;" TO GUESS"
 140 PRINT AT VAL"18",VAL"1";"PRESS ANY KEY TO START A GAME"
 150 PAUSE 0

 199 REM Get Word to Guess
 200 CLS:RANDOMIZE PEEK VAL"23672"*VAL"256"+PEEK VAL"23673"
 210 LET W=VAL"1"+INT(RND*N):LET W=E+W*2:LET W=PEEK W*VAL"256"+PEEK(W+1)
 215 REM PRINT "WORD NUMBER: ";W
 220 POKE A,INT(W/VAL"256"):POKE B,W-VAL"256"*PEEK A:RANDOMIZE USR C
 230 LET R=PEEK A*VAL"256"+PEEK B:IF R=VAL"65535" THEN PRINT "UNEXPECTED ERROR":STOP
 240 LET W$="":FOR J=VAL"1" TO VAL"5":LET W$=W$+CHR$(PEEK (D+J)):NEXT J
 245 REM PRINT"WORD: ";W$

 249 REM Show UI
 250 PRINT AT VAL"15",VAL"6";"Q W E R T Y U I O P"
 260 PRINT AT VAL"17",VAL"6";" A S D F G H J K L"
 270 PRINT AT VAL"19",VAL"6";"  Z X C V B N M ["

 299 REM Guess input routine
 300 LET T=NOT PI:PRINT AT VAL"4",VAL"13";
 310 LET G$="":BORDER P
 320 LET K$=INKEY$:IF K$="" THEN GO TO 320
 330 IF K$=" " THEN LET K$="[":REM ENIE
 340 IF CODE(K$)<>VAL"12" THEN GO TO 370
 350 IF LEN(G$)>VAL"1" THEN LET G$=G$(1 TO (LEN(G$)-1)):PRINT CHR$(VAL"8");" ";CHR$(VAL"8");:GO TO 410
 360 IF LEN(G$)=VAL"1" THEN LET G$="":PRINT CHR$(VAL"8");" ";CHR$(VAL"8");:GO TO 410
 370 IF CODE(K$)=VAL"13" AND LEN(G$)=VAL"5" THEN GO TO 430
 380 IF CODE(K$)>VAL"96" AND CODE(K$)<123 THEN LET K$=CHR$(CODE(K$)-VAL"32"):REM LOWERCASE
 390 IF CODE(K$)<VAL"65" OR CODE(K$)>91 THEN GO TO 320
 400 IF LEN(G$)<VAL"5" THEN LET G$=G$+K$:PRINT K$;
 410 IF INKEY$<>"" THEN GO TO 410
 420 GO TO 320
 430 LET K$=CHR$(VAL"8"):PRINT K$;K$;K$;K$;K$;"     ";K$;K$;K$;K$;K$;
 440 IF INKEY$<>"" THEN GO TO 440

 499 REM Validation of guess
 510 BORDER I
 520 IF G$=W$ THEN INK O:PRINT G$:INK S:PRINT AT VAL"10",VAL"10";"SUCCESS! :-)":INK I:GO TO 670
 530 FOR J=VAL"1" TO VAL"5":POKE(D+J),CODE(G$(J TO J)):NEXT J
 540 POKE A,VAL"255":POKE B,VAL"255":RANDOMIZE USR C
 550 LET R=PEEK A*VAL"256"+PEEK B:IF R=VAL"65535" THEN BORDER K:BEEP .5,NOT PI:GO TO 650:REM NOT A VALID WORD >:-|
 560 LET T=T+VAL"1"

 600 BORDER P:FOR J=VAL"1" TO VAL"5":LET S=K
 610 IF G$(J TO J)=W$(J TO J) THEN S=O:GO TO 640
 620 FOR L=VAL"1" TO VAL"5":IF G$(J TO J)=W$(L TO L) THEN LET S=Y
 630 NEXT L
 640 PAPER I:INK S:PRINT G$(J TO J);:INK I:PAPER P:NEXT J
 650 PRINT AT T+VAL"4",VAL"13";:IF T<VAL"6" THEN GO TO 310
 660 INK K:PRINT AT VAL"11",VAL"10";"YOU LOST!!!":INK I:PRINT AT VAL"12",VAL"9";"IT WAS: ";W$
 670 IF INKEY$<>"" THEN GO TO 670
 680 IF INKEY$="" THEN GO TO 680
 690 GO TO 200
