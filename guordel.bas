#program guordel_en
#autostart

   1 REM Copyright (c) 2022, kounch. All rights reserved.
   2 REM SPDX-License-Identifier: BSD-2-Clause

  10 CLEAR VAL "35839":LET I=NOT PI:LET P=VAL"7":LET O=VAL"4":LET K=VAL"2":LET Y=VAL"6"
  20 LET D=VAL"44024":LET A=D+VAL"6":LET B=A+VAL"1":LET C=VAL"35840":LET E=VAL"39168":LET F=VAL"43776"

  49 REM Get lists length
  50 PAPER P:BORDER P:INK I:CLS:PRINT AT VAL"7",VAL"6";"COUNTING WORDS... ";:POKE A,NOT PI:POKE B,NOT PI:RANDOMIZE USR C:PRINT "OK"
  60 LET M=PEEK A*VAL"256"+PEEK B:PRINT AT VAL"9",VAL"10";"FOUND: ";M
  70 LET N=PEEK E*VAL"256"+PEEK (E+VAL"1"):PRINT AT VAL"11",VAL"10";N;" TO GUESS"
  80 PRINT AT VAL"2",VAL"8";"** ZX GUORDEL **":PRINT AT VAL"21",VAL"8";"(C) kounch  2022"
  90 PRINT AT VAL"16",VAL"8";"PRESS H FOR HELP"
 100 PRINT AT VAL"18",VAL"2";"PRESS ANY OTHER KEY  TO PLAY"
 110 LET K$=INKEY$:IF K$="" THEN GO TO 110
 120 IF INKEY$<>"" THEN GO TO 120
 130 IF K$="H" OR K$="h" THEN GO TO 800
 140 IF K$="S" OR K$="s" OR K$="M" OR K$="m" THEN GO TO 700

 149 REM Precalculate attribute addresses for onscreen keyboard
 150 CLS:PRINT AT VAL"7",VAL"10";"PREPARING...":LET U$="QWERTYUIOPASDFGHJKLZXCVBNM":DIM U(27):DIM V(27)
 160 FOR J=VAL"1" TO VAL"27":LET Z=VAL"15":LET X=VAL"4":LET L=J:IF J>VAL"10" THEN LET Z=VAL"17":LET X=VAL"5":LET L=J-VAL"10"
 170 IF J>VAL"19" THEN LET Z=VAL"19":LET X=VAL"6":LET L=J-VAL"19"
 180 LET X=X+L*VAL"2":LET U(J)=VAL"22528"+Z*VAL"32"+X:NEXT J

 199 REM Get Word to Guess
 200 FOR J=VAL"1" TO VAL"27":LET V(J)=P*VAL"8"+I:NEXT J
 210 CLS:RANDOMIZE PEEK VAL"23672"*VAL"256"+PEEK VAL"23673":LET W=VAL"1"+INT(RND*N):LET W=E+W*2:LET W=PEEK W*VAL"256"+PEEK(W+1)
 229 POKE A,INT(W/VAL"256"):POKE B,W-VAL"256"*PEEK A:RANDOMIZE USR C:LET R=PEEK A*VAL"256"+PEEK B:IF R=VAL"65535" THEN PRINT "UNEXPECTED ERROR":STOP
 230 LET W$="":FOR J=VAL"1" TO VAL"5":LET W$=W$+CHR$(PEEK (D+J)):NEXT J

 249 REM Show UI
 250 PRINT AT VAL"15",VAL"6";"Q W E R T Y U I O P":PRINT AT VAL"17",VAL"6";" A S D F G H J K L"
 260 PRINT AT VAL"19",VAL"6";"  Z X C V B N M"
 270 PLOT VAL"102",VAL"145":DRAW VAL"43",NOT PI:DRAW NOT PI,VAL"-51":DRAW VAL"-43",NOT PI:DRAW NOT PI,VAL"51"
 280 PLOT VAL"43",VAL"59":DRAW VAL"161",NOT PI:DRAW NOT PI,VAL"-49":DRAW VAL"-161",NOT PI:DRAW NOT PI,VAL"49"
 290 PAPER P:INK I:BORDER P

 299 REM Guess input
 300 LET T=NOT PI
 310 LET G$="":BORDER P
 320 LET K$=INKEY$:IF K$="" THEN GO TO 320
 330 PRINT AT VAL"4"+T,VAL"13"+LEN(G$);
 340 IF CODE(K$)<>VAL"12" THEN GO TO 370
 350 IF LEN(G$)>VAL"1" THEN LET G$=G$(1 TO (LEN(G$)-1)):PRINT CHR$(VAL"8");" ";CHR$(VAL"8");:GO TO 410
 360 IF LEN(G$)=VAL"1" THEN LET G$="":PRINT CHR$(VAL"8");" ";CHR$(VAL"8");:GO TO 410
 370 IF CODE(K$)=VAL"13" AND LEN(G$)=VAL"5" THEN GO TO 430
 380 IF CODE(K$)>VAL"96" AND CODE(K$)<123 THEN LET K$=CHR$(CODE(K$)-VAL"32"):REM LOWERCASE
 390 IF CODE(K$)<VAL"65" OR CODE(K$)>91 THEN GO TO 320
 400 IF LEN(G$)<VAL"5" THEN LET G$=G$+K$:PRINT K$;
 410 IF INKEY$<>"" THEN GO TO 410
 420 GO TO 320
 430 PRINT AT VAL"4"+T,VAL"13";
 440 IF INKEY$<>"" THEN GO TO 440

 499 REM Guess validation and UI update
 500 BORDER I:IF G$=W$ THEN BORDER P:INK O:PRINT G$:INK O:PRINT AT VAL"12",VAL"10";"SUCCESS! :-)":INK I:GO TO 600
 510 FOR J=VAL"1" TO VAL"5":POKE(D+J),CODE(G$(J TO J)):NEXT J:POKE A,VAL"255":POKE B,VAL"255":RANDOMIZE USR C
 520 LET R=PEEK A*VAL"256"+PEEK B:IF R=VAL"65535" THEN BORDER K:BEEP .5,NOT PI:PRINT "     ":GO TO 310:REM NOT A VALID WORD >:-|
 530 LET T=T+VAL"1":BORDER P:FOR J=VAL"1" TO VAL"5":LET S=K:IF G$(J TO J)=W$(J TO J) THEN LET S=O:GO TO 560
 540 FOR L=VAL"1" TO VAL"5":IF G$(J TO J)=W$(L TO L) THEN LET S=Y
 550 NEXT L
 560 POKE VAL"22636"+T*VAL"32"+J,I*VAL"8"+S:LET H=0:FOR L=1 TO LEN(U$):IF U$(L TO L)=G$(J TO J) THEN LET H=L
 570 NEXT L:IF V(H)<>O THEN LET V(H)=S
 580 POKE U(H),VAL"8"*I+V(H):NEXT J:INK I:PAPER P:IF T<VAL"6" THEN GO TO 310
 590 INK K:PRINT AT VAL"11",VAL"11";"GAME  OVER":INK I:PRINT AT VAL"12",VAL"9";"IT WAS: ";W$
 600 IF INKEY$<>"" THEN GO TO 600
 610 IF INKEY$="" THEN GO TO 610
 620 GO TO 200
 700 INK P:PAPER VAL"1":BORDER VAL"1":CLS:PRINT AT VAL"10",VAL"2";"Dedicado a Sara y Montse :-*":PAUSE 0:INK I:PAPER P:BORDER P:CLS:GO TO 80

 799 REM HELP TEXT
 800 INK I:PAPER P:BORDER P:CLS:PRINT AT NOT PI,VAL"10";"GUORDEL HELP":PRINT
 810 PRINT "GUESS THE WORD IN SIX TRIES."
 820 PRINT "EACH GUESS MUST BE A VALID FIVE-"
 830 PRINT "LETTER WORD. HIT ENTER TO GUESS."
 840 PRINT "AFTER EACH GUESS, THE COLOUR OF"
 850 PRINT "THE LETTERS WILL CHANGE TO SHOW"
 860 PRINT "HOW CLOSE YOUR GUESS WAS.":PRINT
 870 PRINT AT VAL"9",VAL"12";"EXAMPLES":PRINT
 880 PAPER I:INK O:PRINT "W";:PAPER P:INK I:PRINT"EARY"
 890 PRINT "THE LETTER W IS IN THE WORD AND"
 900 PRINT "IN THE CORRECT SPOT.":PRINT
 910 PRINT "P";:PAPER I:INK Y:PRINT "I";:PAPER P:INK I:PRINT "LLS"
 920 PRINT "THE LETTER I IS IN THE WORD BUT"
 930 PRINT "IN THE WRONG SPOT.":PRINT
 940 PRINT "VAG";:PAPER I:INK K:PRINT "U";:PAPER P:INK I:PRINT "E"
 950 PRINT "THE LETTER U IS NOT IN THE WORD"
 960 PRINT "IN ANY SPOT."
 970 IF INKEY$<>"" THEN GO TO 970
 980 IF INKEY$="" THEN GO TO 980
 990 CLS:GO TO 80
